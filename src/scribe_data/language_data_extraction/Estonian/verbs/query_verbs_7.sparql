# tool: scribe-data
# All Estonian verbs and the corresponding forms per case.
# Enter this query at https://query.wikidata.org/

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?infinitive
  ?presentIndicative
  ?pastIndicative

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .

  # MARK: Infinitive form
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveForm .
    ?infinitiveForm ontolex:representation ?infinitive ;
      wikibase:grammaticalFeature wd:Q179230 .
  }

  # MARK: Present Indicative form
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentIndicativeForm .
    ?presentIndicativeForm ontolex:representation ?presentIndicative ;
      wikibase:grammaticalFeature wd:Q192613 ;
      wikibase:grammaticalFeature wd:Q682111 .
  }

  # MARK: Past Indicative form
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastIndicativeForm .
    ?pastIndicativeForm ontolex:representation ?pastIndicative ;
      wikibase:grammaticalFeature wd:Q1994301 ;
      wikibase:grammaticalFeature wd:Q682111 .
  }
}
