# tool: scribe-data
# All Estonian verbs and their non-finite forms (Des-form, Supine, Infinitive).
# Enter this query at https://query.wikidata.org/

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?des
  ?supine
  ?quotative
  ?impersonal

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .

  # MARK: Des

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?desForm .
    ?desForm ontolex:representation ?des ;
      wikibase:grammaticalFeature wd:Q12360803 .
  }

  # MARK: Supine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?supineForm .
    ?supineForm ontolex:representation ?supine ;
      wikibase:grammaticalFeature wd:Q548470 .
  }

  # MARK: Quotative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?quotativeForm .
    ?quotativeForm ontolex:representation ?quotative ;
      wikibase:grammaticalFeature wd:Q7272884 .
  }

  # MARK: Impersonal

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impersonalForm .
    ?impersonalForm ontolex:representation ?impersonal ;
      wikibase:grammaticalFeature wd:Q1509829 .
  }
}
