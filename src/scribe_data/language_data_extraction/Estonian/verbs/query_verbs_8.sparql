# tool: scribe-data
# All Estonian verbs and the corresponding forms per case.
# Enter this query at https://query.wikidata.org/

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?negativePresent
  ?negativePast
  ?imperfectForm
  ?conditionalPast

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .

  # MARK: Negative Present form
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?negativePresentForm .
    ?negativePresentForm ontolex:representation ?negativePresent ;
      wikibase:grammaticalFeature wd:Q1478451 ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  # MARK: Negative Past form
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?negativePastForm .
    ?negativePastForm ontolex:representation ?negativePast ;
      wikibase:grammaticalFeature wd:Q1478451 ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }

  # MARK: Imperfect Form
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperfectFormForm .
    ?imperfectFormForm ontolex:representation ?imperfectForm ;
      wikibase:grammaticalFeature wd:Q12547192 .
  }

  # MARK: Conditional Past form
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?conditionalPastForm .
    ?conditionalPastForm ontolex:representation ?conditionalPast ;
      wikibase:grammaticalFeature wd:Q625581 ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }
}
