# tool: scribe-data
# All Estonian verbs and the corresponding forms per case.
# Enter this query at https://query.wikidata.org/

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?imperativeSecondSingular
  ?imperativeFirstPlural
  ?imperativeSecondPlural
  ?imperativeImpersonal

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .

  # MARK: Imperative Second Person Singular
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondSingularForm .
    ?imperativeSecondSingularForm ontolex:representation ?imperativeSecondSingular ;
      wikibase:grammaticalFeature wd:Q22716 ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  # MARK: Imperative First Person Plural
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeFirstPluralForm .
    ?imperativeFirstPluralForm ontolex:representation ?imperativeFirstPlural ;
      wikibase:grammaticalFeature wd:Q22716 ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  # MARK: Imperative Second Person Plural
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPluralForm .
    ?imperativeSecondPluralForm ontolex:representation ?imperativeSecondPlural ;
      wikibase:grammaticalFeature wd:Q22716 ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  # MARK: Impersonal Imperative
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeImpersonalForm .
    ?imperativeImpersonalForm ontolex:representation ?imperativeImpersonal ;
      wikibase:grammaticalFeature wd:Q22716 ;
      wikibase:grammaticalFeature wd:Q1509829 .
  }
}
