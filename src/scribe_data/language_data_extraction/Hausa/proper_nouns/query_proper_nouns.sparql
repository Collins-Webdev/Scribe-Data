# tool: scribe-data
# All Hausa (Q56475) proper nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?singular
  ?gender
  ?definite
  ?vocative

WHERE {
  ?lexeme dct:language wd:Q56475 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?singular .
  FILTER(lang(?singular) = "ha")
  # FILTER(lang(?singular) = "ha-arabic")

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  # MARK: Definite form

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteForm .
    ?definiteForm ontolex:representation ?definite ;
      wikibase:grammaticalFeature wd:Q53997851 .
    FILTER(lang(?definite) = "ha")
    # FILTER(lang(?definite) = "ha-arabic")
  }

  # MARK: Vocative form

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeForm .
    ?vocativeForm ontolex:representation ?vocative ;
      wikibase:grammaticalFeature wd:Q185077 .
    FILTER(lang(?vocative) = "ha")
    # FILTER(lang(?vocative) = "ha-arabic")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}