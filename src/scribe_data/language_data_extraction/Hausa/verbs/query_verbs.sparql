# tool: scribe-data
# All Hausa (Q56475) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?imperativeSingular
  ?imperativePlural
  ?past
  ?present

WHERE {
  ?lexeme dct:language wd:Q56475 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .
  FILTER(lang(?infinitive) = "ha")
  # FILTER(lang(?infinitive) = "ha-arabic")

  # MARK: Past

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastForm .
    ?pastForm ontolex:representation ?past ;
      wikibase:grammaticalFeature wd:Q1240211 .
    FILTER(lang(?past) = "ha")
    # FILTER(lang(?past) = "ha-arabic")
  }

  # MARK: Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
    FILTER(lang(?present) = "ha")
    # FILTER(lang(?present) = "ha-arabic")
  }

  # MARK: Imperative Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSingularForm .
    ?imperativeSingularForm ontolex:representation ?imperativeSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q110786 .
    FILTER(lang(?imperativeSingular) = "ha")
    # FILTER(lang(?imperativeSingular) = "ha-arabic")
  }

  # MARK: Imperative Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativePluralForm .
    ?imperativePluralForm ontolex:representation ?imperativePlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q146786 .
    FILTER(lang(?imperativePlural) = "ha")
    # FILTER(lang(?imperativePlural) = "ha-arabic")
  }
}